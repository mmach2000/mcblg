---
title: TOT 草稿
mtime: '2024-10-27T18:49:30+08:00'
ctime: '2024-09-17T18:20:15+08:00'
draft: true
license: CC0-1.0
---
import OriginalText from "@/components/astro/markdown/OriginalText.astro"

import '@/style/apl.css';

<OriginalText>
  **3\. Representations**

  The subjects of mathematical analysis and computation can be *represented* in a variety of ways, and each representation may possess particular advantages. For example, a positive integer `n` may be represented simply by `n` check-marks; less simply, but more compactly, in Roman numerals; even less simply, but more conveniently for the performance of addition and multiplication, in the decimal system; and less familiarly, but more conveniently for the computation of the least common multiple and the greatest common divisor, in the prime decomposition scheme to be discussed here.

  Graphs, which concern connections among a collection of elements, are an example of a more complex entity which possesses several useful representations. For example, a simple directed graph of `n` elements (usually called *nodes*) may be represented by an `n` by `n` boolean matrix `b` (usually called an *adjacency* matrix) such that `b[i;j]=1` if there is a connection from node `i` to node `j` . Each connection represented by a `1` in `b` is called an *edge*, and the graph can also be represented by a `+/,b` by `n` matrix in which each row shows the nodes connected by a particular edge.
</OriginalText>

## 第三节 表示法

数学中，待分析或计算的研究对象往往可以使用多种方式进行**表示**，每种表示法都有其独特优势。
以正整数 `n` 的表示为例：最简单的方法是画 `n` 个对勾；罗马数字稍复杂一点，但更紧凑；
十进制表示法更为复杂，但进行加减乘除时更为方便；
我们还将讨论另一种不太常见的表示方法，即素因子分解表示法，它在计算最大公约数和最小公倍数时尤为方便。

像这样有多种表示方法的数学对象还有很多，图就是其中之一。
图用于描述一组元素之间的连接关系。
例如，我们可以用一个 `n` 乘 `n` 的布尔值矩阵表示一个由 `n` 个元素（一般称之为**节点**）组成的简单有向图。
其中，若矩阵的元素 `b[i][j]` 取值为 `1`，则存在一条从节点 `i` 到节点 `j` 的连接（一般称之为**边**）。
这种矩阵一般称为**邻接矩阵**。
同一张图也可以使用另一种矩阵表示，这种矩阵行数为 `+/,b` ，列数为 `n`，每一行对应一条边。

<OriginalText>
  Functions also admit different useful representations. For example, a permutation function, which yields a reordering of the elements of its vector argument `x` , may be represented by a *permutation vector* `p` such that the permutation function is simply `x[p]` , by a *cycle* representation which presents the structure of the function more directly. by the boolean matrix `b←p∘.=⍳⍴p` such that the permutation function is `b+.×x` , or by a *radix* representation `r` which employs one of the columns of the matrix `1+(⌽⍳n)⊤¯1+!n←⍴x` , and has the property that `2|+/r-1` is the parity of the permutation represented.

  In order to use different representations conveniently, it is important to be able to express the transformations between representations clearly and precisely. Conventional mathematical notation is often deficient in this respect, and the present section is devoted to developing expressions for the transformations between representations useful in a variety of topics: number systems, polynomials, permutations, graphs, and boolean algebra.
</OriginalText>

函数也可以有多种表示方法。
以置换函数（将向量 `x` 中元素重新排列的函数）为例，
每个置换函数都可以表示为一个**置换向量** `p`，那么这个函数的应用就可以表示为 `x[p]`；
或者使用**循环表示法**以更直观的展现函数的结构；
或者使用**布尔矩阵** `b←p∘.=⍳⍴p` ，函数应用表示为 `b+.×x`；
或者使用**基数表示法** `r`，其中矩阵 `1+(⌽⍳n)⊤¯1+!n←⍴x` 的一列对应于置换的一个循环，这种表示法的一个特点是 `2|+/r-1` 的值是置换的奇偶性。

要想灵活地使用这些表示方法，关键在于如何准确而清晰地描述它们之间的转换关系。
在这方面，传统的数学符号往往有所欠缺。
接下来的几节将讨论在数字系统、多项式、置换、图、布尔代数中如何表示这种转换关系。

<OriginalText>
  **3.1 Number Systems**

  We will begin the discussion of representations with a familiar example, the use of different representations of positive integers and the transformations between them. Instead of the *positional* or *base-value* representations commonly treated, we will use *prime decomposition*, a representation whose interesting properties make it useful in introducing the idea of logarithms as well as that of number representation \[6, Ch.16\].

  If `p` is a vector of the first `⍴p` primes and `e` is a vector of non-negative integers, then `e` can be used to represent the number `p×.*e` , and all of the integers `⍳⌈/p` can be so represented. For example, `2 3 5 7 ×.* 0 0 0 0` is `1` and `2 3 5 7 ×.* 1 1 0 0` is `6` and:

  ```
        p
  2 3 5 7
        me
  0 1 0 2 0 1 0 3 0 1
  0 0 1 0 0 1 0 0 2 0
  0 0 0 0 1 0 0 0 0 1
  0 0 0 0 0 0 1 0 0 0
        p×.*me
  1 2 3 4 5 6 7 8 9 10
  ```
</OriginalText>

### 3.1 数的表示

我们先来考虑正整数的表示法。
本节将使用**质因数分解**表示正整数，而非平时使用的**进制表示法**。
质因数分解表示法不仅有助于理解对数，还有助于理解其他的数字表示法。

令 `p` 表示前 `⍴p` 个质数，`e` 是自然数组成的数组，那么 `e` 也可以看作数字 `p×.*e` 的一个表示。
这种表示法能够表示所有属于 `⍳⌈/p` 之中的整数。
例如，`2 3 5 7 ×.* 0 0 0 0` 等于 `1`，而 `2 3 5 7 ×.* 1 1 0 0` 等于 `6`，而：

```apl
      p
2 3 5 7
      me
0 1 0 2 0 1 0 3 0 1
0 0 1 0 0 1 0 0 2 0
0 0 0 0 1 0 0 0 0 1
0 0 0 0 0 0 1 0 0 0
      p×.*me
1 2 3 4 5 6 7 8 9 10
```

<details>
  <summary>
    提示
  </summary>
  ```apl
        ⌈/p
  7
        ⍳⌈/p
  1 2 3 4 5 6 7
  ```
</details>

<OriginalText>
  The similarity to logarithms can be seen in the identity:

  ```
        ×/p×.*me ←→ p×.*+/me
  ```

  which may be used to effect multiplication by addition.

  Moreover, if we define `gcd` and `lcm` to give the greatest common divisor and least common multiple of elements of vector arguments, then:

  ```
        gcd p×.*me ←→ p×.*⌊/me
        lcm p×.*me ←→ p×.*⌈/me

        me          v←p×.*me
  2 1 0             v
  3 1 2       18900 7350 3087
  2 2 0             gcd v                   lcm v
  1 3 4       21                      926100
                    p×.*⌊/me                p×.*⌈/me
              21                      926100
  ```
</OriginalText>

下面的等式揭示了这种表示法和对数的内在相似性：

```apl
        ×/p×.*me ←→ p×.*+/me
```

二者都将乘法转换成了某种加法。

更进一步，在定义最大公约数 `gcd` 和最小公倍数 `lcm` 之后，就有

```apl
      gcd p×.*me ←→ p×.*⌊/me
      lcm p×.*me ←→ p×.*⌈/me
```

```apl
      me
2 1 0
3 1 2
2 2 0
1 3 4
      v←p×.*me
      v
18900 51450 21609
      gcd v
21
      p×.*⌊/me 
21
      lcm v
6482700
      p×.*⌈/me
6482700
```

<details>
  <summary>
    提示
  </summary>
  ```apl  
        ⌊/me
  0 1 0 1
        ⌈/me
  2 3 2 4
  ```
</details>

<OriginalText>
  In defining the function `gcd` , we will use the operator `/` with a boolean argument `b` (as in `b/`). It produces the *compression* function which selects elements from its right argument according to the *ones* in `b` . For example, `1 0 1 0 1/⍳5` is `1 3 5` . Moreover, the function `b/` applied to a matrix argument compresses rows (thus selecting certain columns), and the function `b⌿` compresses columns to select rows. Thus:

  ```
        gcd:gcd m,(m←⌊/r)|r:1≥⍴r←(⍵≠0)/⍵:+/r
        lcm:(×/x)÷gcd x←(1↑⍵),lcm 1↓⍵:0=⍴⍵:1
  ```

  The transformation to the value of a number from its prime decomposition representation (`vfr`) and the inverse transformation to the representation from the value (`rpv`) are given by:

  ```
        vfr:⍺×.*⍵
        rfv:d+⍺ rfv ⍵÷⍺×.*d:^/~d←0=⍺|⍵:d
  ```

  For example:

  ```
        p vfr 2 1 3 1
  10500
        p rfv 10500
  2 1 3 1
  ```
</OriginalText>

下面是 `gcd` 和 `lcm` 的定义。这里我们使用了**压缩**函数，这一函数形如 `b/`，由布尔参数 `b` 和算子 `/` 组合而成。
其作用是根据 `b` 中 `1` 的位置选取元素。例如，`1 0 1 0 1/⍳5` 等于 `1 3 5`。
更进一步，这一函数也可以用于矩阵（这种情况下就是选择特定的列），`b⌿` 则作用于列（也就是选择特定的行）。

```apl
      gcd:gcd m,(m←⌊/r)|r:1≥⍴r←(⍵≠0)/⍵:+/r
      lcm:(×/x)÷gcd x←(1↑⍵),lcm 1↓⍵:0=⍴⍵:1
```

然后是将数字转换为质因数分解的函数 `rfv` 和将质因数分解转回数字的函数 `vfr`：

```apl
      vfr:⍺×.*⍵
      rfv:d+⍺ rfv ⍵÷⍺×.*d:^/~d←0=⍺|⍵:d
```
```apl
      p vfr 2 1 3 1
10500
      p rfv 10500
2 1 3 1
```
